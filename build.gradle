plugins {
    id 'java'
    id 'java-library'
    id 'checkstyle'
    id 'org.springframework.boot' version '3.5.4' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
}

allprojects {
    group = 'ru.oldzoomer'
    version = '0.0.1-SNAPSHOT'
    
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url = uri("https://maven.pkg.github.com/oldzoomer-ru/nodelistj/")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'checkstyle'
    
    if (!project.path.startsWith(':lib')) {
        apply plugin: 'org.springframework.boot'
    } else {
        apply plugin: 'java-library'
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    ext {
        mapstructVersion = '1.6.3'
        therapiVersion = '0.15.0'
        nodelistjVersion = '1.0'
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.testcontainers:postgresql'
        testImplementation 'org.testcontainers:kafka'
        testImplementation 'org.testcontainers:minio'
        testImplementation 'com.redis:testcontainers-redis'
        testImplementation 'org.testcontainers:junit-jupiter'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    test {
        useJUnitPlatform()
    }
}