plugins {
    id 'java'
    id 'java-library'
    id 'checkstyle'
    id 'org.springframework.boot' version '4.0.1' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
}

allprojects {
    group = 'ru.oldzoomer'
    version = '0.0.1-SNAPSHOT'
    
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url = uri("https://maven.pkg.github.com/oldzoomer-ru/nodelistj/")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME") ?: ""
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN") ?: ""
            }
        }
    }
}

subprojects {
    if (project.path.startsWith(':lib')) {
        apply plugin: 'java-library'
    } else {
        apply plugin: 'java'
    }

    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'checkstyle'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:4.0.1"
        }
    }

    if (!project.path.startsWith(':lib')) {
        apply plugin: 'org.springframework.boot'
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(25)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    ext {
        mapstructVersion = '1.6.3'
        therapiVersion = '0.15.0'
        nodelistjVersion = '1.0.2'
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        implementation 'org.springframework.boot:spring-boot-configuration-processor'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    test {
        useJUnitPlatform()
    }
}